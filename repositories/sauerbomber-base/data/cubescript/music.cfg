musicosdduration = 15000

getcurrentcover = [
  result (concatword "packages/" $musicdir "/" $currentartist "/" $currentalbum "/" $albumcover)
]

musicosd = [
  clearosd
  addosdimage (getcurrentcover) 1 82 500 0.5 1.0 (* $musicosdduration 1.2)
  addosdtext $currentartist 5 43 1.0 1.0 $musicosdduration
  addosdtext $currentalbum 5 45 1.0 1.0 $musicosdduration
  addosdtext $currentsong 5 47 1.0 1.0 $musicosdduration
]

musicosdstopped = [
  addosdimage (getcurrentcover) 1 82 500 0.5 1.0 (* $musicosdduration 1.2)
  addosdtext $currentartist 5 43 1.0 1.0 $musicosdduration
  addosdtext $currentalbum 5 45 1.0 1.0 $musicosdduration
  addosdtext "STOPPED" 5 47 1.0 1.0 $musicosdduration
]

playnextartist = [
  nextartist "playnextsong" "musicosd" "musicosdstopped"
]

playprevioussong = [
  previoussong "playnextsong" "musicosd" "musicosdstopped"
]

playnextsong = [
  nextsong "playnextsong" "musicosd" "musicosdstopped"
]
playnextsong
playasong = playnextsong

volumeosdduration = 200

volumeosd = [
  _volpercent = (divf $musicvol 2.55)
  _volstr = ""
  loop _voli 15 [
    if (>= $_volpercent (*f $_voli 6.67)) [
      _volstr = (concatword $_volstr "O")
      addosdimage "packages/skins/sauerbomber/osdvolume.png" (+ (* $_voli 3) 35) 150 200 0.3 0.6 $volumeosdduration
    ]
  ]
]

volumeup = [
  _volup = (+ $musicvol 15)
  if (> $_volup 255) [
    _volup = 255
  ]
  musicvol $_volup
  volumeosd
]

volumedown = [
  _voldown = (- $musicvol 15)
  if (< $_voldown 0) [
    _voldown = 0
  ]
  musicvol $_voldown
  volumeosd
]

// classic key repeat functionalities in cubescript!

_volumechanging = -1
_volumechangekeypress = 250
_volumechangekeyrepeat = 60

volumechange = [
  _volumechangekeydelay = $_volumechangekeyrepeat
  if (< $arg2 0) [
    _volumechanging = 0
  ]
  if (> $arg2 0) [
    _volumechanging = 1
    _volumechangekeydelay = $_volumechangekeypress
  ]
  if (= $_volumechanging 1) [
    do @arg1
    sleep @_volumechangekeydelay [
      volumechange @@arg1
    ]
  ]
]

_songchanging = -1
_songchangekeypress = 2500
_songchangekeyrepeat = 1500

songchange = [
  _songchangekeydelay = $_songchangekeyrepeat
  if (< $arg3 0) [
    _songchanging = 0
    _songchangemillis = (getmillis)
  ]
  if (> $arg3 0) [
    _songchanging = 1
    _songchangekeydelay = $_songchangekeypress
  ]
  if (= $_songchanging 1) [
    do @arg1
    _songchangemillis = (getmillis)
    sleep @_songchangekeydelay [
      if (= @_songchangemillis $_songchangemillis) [
        songchange @@@arg2 @@@arg2
      ]
    ]
  ]
]

bind KP_PLUS [ volumechange volumeup 1; onrelease [ volumechange volumeup -1 ] ]
bind KP_MINUS [volumechange volumedown 1; onrelease [ volumechange volumedown -1 ] ]
bind KP_DIVIDE playprevioussong
bind KP_MULTIPLY [songchange playnextsong playnextartist 1; onrelease [ songchange 0 0 -1 ] ]
