sbng_is_ctf = [ result (>= (indexof [11 12 17] $arg1) 0) ]
sbng_is_instagib = [ result (>= (indexof [3 4] $arg1) 0) ]
sbng_is_bomberman = [ result (>= (indexof [24 25] $arg1) 0) ]
sbng_is_race = [ result (= $arg1 26) ]
sbng_is_hideandseek = [ result (>= (indexof [27 28 29] $arg1) 0) ]
sbng_is_regencapture = [ result (= $arg1 10) ]
sbng_is_collect = [ result (>= (indexof [20 21 22] $arg1) 0) ]
sbng_is_protect = [ result (>= (indexof [13 14 18] $arg1) 0) ]
sbng_is_hold = [ result (>= (indexof [15 16 19] $arg1) 0) ]
sbng_is_tactics = [ result (>= (indexof [7 8] $arg1) 0) ]
sbng_is_effic = [ result (>= (indexof [5 6 17 18 19 22] $arg1) 0) ]
sbng_is_insta = [ result (>= (indexof [3 4 12 14 16 21 28] $arg1) 0) ]
sbng_is_coop = [ result (= $arg1 1) ]

newgui serverbrowserng [
  guinoautotab [
    guistayopen [
      sbng_tab "CaptureTheFlag" sbng_is_ctf -1
      sbng_tab "Instagib" sbng_is_instagib
      sbng_tab "Bomberman" sbng_is_bomberman
      sbng_tab "Race" sbng_is_race
      sbng_tab "Hide & Seek" sbng_is_hideandseek
      sbng_tab "Regen Capture" sbng_is_regencapture
      sbng_tab "Collect" sbng_is_collect
      sbng_tab "Protect" sbng_is_protect
      sbng_tab "Hold" sbng_is_hold
      sbng_tab "Tactics" sbng_is_tactics
      sbng_tab "Effic" sbng_is_effic
      sbng_tab "Insta" sbng_is_insta
      sbng_tab "Coop Edit" sbng_is_coop
    ]
  ]
] "CaptureTheFlag"

sbng_tab = [
  if (>= $arg3 0) [
    guitab $arg1
  ]
  _sbng_mode = ""
  _sbng_map = ""
  _sbng_players = ""
  _sbng_description = ""
  loopservers __sbng_host __sbng_port __sbng_mode __sbng_map __sbng_mastermode __sbng_players __sbng_description [
    if (= ($arg2 $__sbng_mode) 1) [
      _sbng_mode = (concat $_sbng_mode (escape (at $modenames @__sbng_mode)))
      _sbng_map = (concat $_sbng_map (escape @__sbng_map))
      _sbng_players = (concat $_sbng_players (escape @__sbng_players))
      _sbng_description = (concat $_sbng_description "[ guibutton" (escape @__sbng_description) "[ connect" @__sbng_host @__sbng_port "] 0 ]")
    ]
  ]
  guititle (concat $arg1 "servers")
  guibar
  guilist [
    guistrut 2
    guilist [
      guitext "Mode"
      guibar
      looplist _mode $_sbng_mode [
        guitext $_mode 0
      ]
    ]
    guistrut 2
    guilist [
      guitext "Map"
      guibar
      looplist _map $_sbng_map [
        guitext $_map 0
      ]
    ]
    guistrut 2
    guilist [
      guitext "Players"
      guibar
      looplist _players $_sbng_players [
        guitext $_players 0
      ]
    ]
    guistrut 2
    guilist [
      guitext "Server"
      guibar
      looplist _description $_sbng_description [
        do $_description
      ]
    ]
    guistrut 2
  ]
]
